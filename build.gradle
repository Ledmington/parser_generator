plugins {
    id 'java'
    id 'jacoco'
    id 'pmd'
    id 'com.adarshr.test-logger' version '4.0.0'
    id 'com.diffplug.spotless' version '7.0.4'
    id 'com.github.ben-manes.versions' version '0.52.0'
    id 'de.undercouch.download' version '5.6.0'
}

static def path(String... args) {
    return String.join(File.separator, args)
}

// Tunables
ext.basePackage = 'com.ledmington'
def version = '0.1.0'
String appDescription = 'Parser Generator'
String author = 'Filippo Barbari'
String authorMail = 'filippo.barbari@gmail.com'
String appName = 'parser-gen'
ext.appNameLowerCase = appName.toLowerCase(Locale.US)

ext.junitVersion = '5.13.1'
ext.junitLauncherVersion = '1.13.1'
ext.jacocoVersion = '0.8.12'
ext.pmdVersion = '7.13.0'
ext.palantirVersion = '2.69.0'
// End tunables

allprojects {
    apply plugin: 'java'
    apply plugin: 'jacoco'
    apply plugin: 'pmd'
    apply plugin: 'com.adarshr.test-logger'
    apply plugin: 'com.github.ben-manes.versions'

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(21)
        }
    }

    repositories {
        mavenCentral()
    }

    dependencies {
        testImplementation "org.junit.jupiter:junit-jupiter-api:${junitVersion}"
        testImplementation "org.junit.jupiter:junit-jupiter-params:${junitVersion}"
        testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${junitVersion}"
        testRuntimeOnly "org.junit.platform:junit-platform-launcher:${junitLauncherVersion}"
    }

    tasks.withType(JavaCompile).configureEach {
        options.compilerArgs << '-Xdiags:verbose'
        options.compilerArgs << '-Xlint:all'
        options.compilerArgs << '-Werror'
        options.deprecation = true
        options.encoding = 'UTF-8'
    }

    javadoc {
        failOnError = true
        title = "parser-generator-v${version}-doc"
        options.encoding = 'UTF-8'
        options.addBooleanOption('Werror', true)
    }

    test {
        useJUnitPlatform()
        finalizedBy jacocoTestReport
    }

    jacocoTestReport {
        dependsOn test // tests are required to run before generating the report
        reports {
            html.required = true
            html.outputLocation = layout.buildDirectory.dir('jacoco')
        }
    }

    jacoco {
        toolVersion = "${jacocoVersion}"
        reportsDirectory = layout.buildDirectory.dir('jacoco')
    }

    testlogger {
        theme = 'standard-parallel'

        showExceptions = true
        showStackTraces = true
        showFullStackTraces = false
        showCauses = true

        slowThreshold = 2000

        showSummary = true
        showSimpleNames = false
        showPassed = false // enable for verbose tests
        showSkipped = true
        showFailed = true
        showOnlySlow = false

        showStandardStreams = true
        showPassedStandardStreams = false
        showSkippedStandardStreams = true
        showFailedStandardStreams = true

        logLevel = 'lifecycle'
    }

    // Common config for all PMD tasks
    pmd {
        toolVersion = "${pmdVersion}"
        ignoreFailures = false
        consoleOutput = true
        rulesMinimumPriority = 5
        threads = 1 // Each PMD instance for each module runs serially
        incrementalAnalysis = true
    }

    // cleanup tasks
    clean.dependsOn('cleanBin')

    // remove default VSCode build directory
    tasks.register('cleanBin', Delete) {
        delete "${project.projectDir}/bin"
    }
}

spotless {
    format 'misc', {
        target '*.md', '.gitignore', '.gitattributes'
        trimTrailingWhitespace()
        leadingSpacesToTabs()
        endWithNewline()
        setEncoding('utf-8')
    }

    java {
        target fileTree('.') {
            include path('cli', '**', '*.java')
            include path('ebnf', '**', '*.java')
            include path('generator', '**', '*.java')
            exclude path('**', 'build', '**'), path('**', 'build-*', '**'), path('**', 'bin', '**')
        }
        setEncoding('utf-8')
        removeUnusedImports()
        palantirJavaFormat("${palantirVersion}").formatJavadoc(true)
        leadingSpacesToTabs()
        formatAnnotations()
        trimTrailingWhitespace()
        endWithNewline()

        // check https://javadoc.io/doc/com.diffplug.spotless/spotless-plugin-gradle/latest/com/diffplug/gradle/spotless/JavaExtension.html
        importOrder('java', 'javax', 'javafx', 'org.junit', 'org.openjdk', 'com', 'com.ledmington', 'org', group as String)

        licenseHeader String.join("\n",
                "/*",
                " * ${appName} - ${appDescription}",
                " * Copyright (C) 2025-${new Date().format('yyyy')} ${author} <${authorMail}>",
                " *",
                " * This program is free software: you can redistribute it and/or modify",
                " * it under the terms of the GNU General Public License as published by",
                " * the Free Software Foundation, either version 3 of the License, or",
                " * (at your option) any later version.",
                " *",
                " * This program is distributed in the hope that it will be useful,",
                " * but WITHOUT ANY WARRANTY; without even the implied warranty of",
                " * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the",
                " * GNU General Public License for more details.",
                " *",
                " * You should have received a copy of the GNU General Public License",
                " * along with this program.  If not, see <http://www.gnu.org/licenses/>.",
                " */",
        )
    }
}

allprojects.each {
    it.tasks.compileJava.dependsOn(spotlessApply)
    it.tasks.build.dependsOn(it.tasks.javadoc)
}
